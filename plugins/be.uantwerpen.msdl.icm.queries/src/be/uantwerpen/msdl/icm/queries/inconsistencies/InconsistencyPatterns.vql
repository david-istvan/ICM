package be.uantwerpen.msdl.icm.queries.inconsistencies

import "metamodels.process"
import pattern be.uantwerpen.msdl.icm.queries.general.nodeGloballyReachableFromNode
import pattern be.uantwerpen.msdl.icm.queries.general.intent

pattern sharedProperty(activity1: Activity, intent1: Intent, activity2: Activity, intent2: Intent, property: Property){
	find intent(activity1, property, intent1);
	find intent(activity2, property, intent2);
	find nodeGloballyReachableFromNode(activity2, activity1);
}

pattern readModifySharedProperty(activity1: Activity, activity2: Activity, property: Property){
	find sharedProperty(activity1, intent1, activity2, intent2, property);
	Intent.type(intent1, IntentType::READ);
	Intent.type(intent2, IntentType::MODIFY);
}

pattern readModifySharedProperty2(activity1: Activity, activity2: Activity, property: Property, process: Process){
	find readModifySharedProperty(activity1, activity2, property);
	Process.node(process, activity1);
	Process.node(process, activity2);
}

pattern forkOutControl(forkNode : Fork){
	Fork.controlOut(forkNode,_);
}