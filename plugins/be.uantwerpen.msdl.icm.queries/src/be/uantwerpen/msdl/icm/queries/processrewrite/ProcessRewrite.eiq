package be.uantwerpen.msdl.icm.queries.processrewrite

import "metamodels.process"

/**
 * Activities are connected via a control flow
 */
pattern controlFlowBetweenNodes(node1: Node, node2: Node, controlFlow: ControlFlow){
	ControlFlow.fromNode(controlFlow, node1);
	ControlFlow.toNode(controlFlow, node2);
}

/**
 * Hard control flow constraint
 */
pattern hardControlFlowBetweenActivities(activity1: Activity, activity2: Activity, controlFlow: ControlFlow){
	find controlFlowBetweenNodes(activity1, activity2, controlFlow);
	ControlFlow.final(controlFlow, f);
	f == true;
}

/**
 * Soft control flow constraint
 */
pattern softControlFlowBetweenActivities(activity1: Activity, activity2: Activity, controlFlow: ControlFlow){
	find controlFlowBetweenNodes(activity1, activity2, controlFlow);
	ControlFlow.final(controlFlow, f);
	f != true;
}

/**
 * Activities w/o control flow constraint
 */
pattern independentNodes(process: Process, node1: Node, node2: Node){
	Process.node(process, node1);
	Process.node(process, node2);
	node1!=node2;
	neg find controlFlowBetweenNodes(node1, node2, _);
}

//pattern independentNodes2(node1: Node, node2: Node){
//	find independentNodes(node1, node2);
//}

/**
 * Object flow
 */
pattern objectFlowBetweenIndependentActivities(activity1: Activity, activity2: Activity){
	find controlFlowBetweenNodes(activity1, activity2, _);
	
	Object.dataFlowFrom(object, activity1);
	Object.dataFlowTo(object, activity2);
}

pattern intent(activity: Activity, property: Property, intent: Intent){
	Intent.activity(intent, activity);
	Intent.subjectOfIntent(intent, property);
}

/**
 * Two activities touch the same property and they have a shared object.
 * TODO this is not enough: types of intents lead back to temporal tolerance!!!
 */
pattern sharedProperty(activity1: Activity, activity2: Activity, property: Property){
	find intent(activity1, property, _);
	find intent(activity2, property, _);
}

pattern forkOutControl(forkNode : Fork){
	Fork.toControlFlow(forkNode,_);
}